apply plugin: 'com.android.application'

ext {
    codeNumber = Integer.parseInt(appCodeNumber)
    versionNumber = appVersionNumberBase + "." + appVersionNumberIndex
    db = Integer.parseInt(appDB)
    println ""
    println "Version: [" + versionNumber + " - " + codeNumber + "]"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.foobnix.pdf.reader"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode codeNumber
        versionName versionNumber

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    signingConfigs {

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "LOG", "false"
        }
        debug {
            buildConfigField "boolean", "LOG", "true"
        }


    }
    buildToolsVersion '28.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        // Configures multiple APKs based on ABI.
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk false
        }

    }


    flavorDimensions "version"
    productFlavors {
        beta {
            dimension "version"
            applicationId "com.foobnix.pdf.reader"
            manifestPlaceholders = [
                    appName     : "Beta " + versionNumber,
                    appIcon     : "@mipmap/icon_pdf_reader",
                    appRoundIcon: "@mipmap/icon_pdf_reader",
                    appAdmobId  : "ca-app-pub-3940256099942544~3347511713"
            ]
            buildConfigField "String", "YOUR_ADMOB_APP_ID", '"ca-app-pub-3940256099942544~3347511713"'
        }
        librera {
            dimension "version"
            applicationId "com.foobnix.pdf.reader"
            manifestPlaceholders = [
                    appName     : "Librera Reader",
                    appIcon     : "@mipmap/icon_pdf_reader",
                    appRoundIcon: "@mipmap/icon_pdf_reader"
            ]
        }
        pro {
            dimension "version"
            applicationId "com.foobnix.pro.pdf.reader"
            manifestPlaceholders = [
                    appName     : "Librera PRO",
                    appIcon     : "@mipmap/icon_pdf_pro",
                    appRoundIcon: "@mipmap/icon_pdf_pro"
            ]
        }
        pdf_classic {
            dimension "version"
            applicationId "classic.pdf.reader.viewer.djvu.epub.fb2.txt.mobi.book.reader.lirbi.libri"
            manifestPlaceholders = [
                    appName     : "PDF Reader",
                    appIcon     : "@mipmap/icon_pdf_classic",
                    appRoundIcon: "@mipmap/launcher_classic"
            ]
        }
        ebooka {
            dimension "version"
            applicationId "droid.reader.book.epub.mobi.pdf.djvu.fb2.txt.azw.azw3"
            manifestPlaceholders = [
                    appName     : "Ebooka Reader",
                    appIcon     : "@mipmap/icon_pdf_droid",
                    appRoundIcon: "@mipmap/launcher_droid"
            ]
        }
        pdf_v2 {
            dimension "version"
            applicationId "pdf.pdf.reader"
            manifestPlaceholders = [
                    appName     : "PDF Reader",
                    appIcon     : "@mipmap/icon_pdf_2",
                    appRoundIcon: "@mipmap/launcher_pdf_v2"
            ]
        }
        tts_reader {
            dimension "version"
            applicationId "tts.reader"
            manifestPlaceholders = [
                    appName     : "TTS Reader",
                    appIcon     : "@mipmap/icon_tts_reader_app",
                    appRoundIcon: "@mipmap/launcher_tts_reader_app"
            ]
        }
        epub_reader {
            dimension "version"
            applicationId "epub.reader"
            manifestPlaceholders = [
                    appName     : "Epub Reader",
                    appIcon     : "@mipmap/icon_epub_reader",
                    appRoundIcon: "@mipmap/launcher_epub_reader"
            ]
        }
        fdroid {
            dimension "version"
            applicationId "com.foobnix.pro.pdf.reader"
            manifestPlaceholders = [
                    appName     : "Librera PRO",
                    appIcon     : "@mipmap/icon_pdf_pro",
                    appRoundIcon: "@mipmap/icon_pdf_pro"
            ]
            versionNameSuffix '-fdroid'
        }

    }


    def abiCodes = ['armeabi-v7a': 0, 'arm64-v8a': 1, 'x86': 2, 'x86_64': 3]

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavor = variant.productFlavors[0].name.capitalize()
            def abiName = getFilter("ABI")
            def code = abiCodes.get(abiName, 0) + variant.versionCode
            abiName = abiName.replace("arm64-v8a", "arm64").replace("armeabi-v7a", "arm")

            output.versionCodeOverride = code
            output

            def fullName = "Librera ${flavor}-${versionNumber}-c${code}-d${db}-${abiName}.apk";
            output.outputFileName = fullName;

        }
    }


}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'com.android.support:appcompat-v7:28+'
    implementation 'com.android.support:recyclerview-v7:28+'
    implementation 'com.android.support:cardview-v7:28+'
    implementation 'com.android.support:support-v4:28+'
    implementation 'com.android.support:customtabs:28+'

    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:greendao-api:3.2.2'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.burgstaller:okhttp-digest:1.18'
    implementation 'com.squareup.okio:okio-parent:1.17.3'
    implementation 'com.github.joniles:rtfparserkit:1.13.0'


    implementation 'org.zwobble.mammoth:mammoth:1.4.0'
    implementation 'javax.xml.stream:stax-api:1.0-2'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    proImplementation project(':pro')
    fdroidImplementation project(':pro')

    //betaImplementation project(':pro')
    betaImplementation 'com.google.android.gms:play-services-ads:16.0.0'

    libreraImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    pdf_v2Implementation 'com.google.android.gms:play-services-ads:16.0.0'
    ebookaImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    tts_readerImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    pdf_classicImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    epub_readerImplementation 'com.google.android.gms:play-services-ads:16.0.0'


    proImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    betaImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    libreraImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    pdf_v2Implementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    ebookaImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    tts_readerImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    pdf_classicImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    epub_readerImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
}


task copyApks(type: Copy) {
    if (project.hasProperty('beta')) {
        println "My Task: [copy apks]"

        delete fileTree("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing").include("*.apk")

        from file("build/outputs/apk/beta/release/")
        include "*.apk"
        into file("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing/")

        File file = new File('/home/ivan-dev/Dropbox/FREE_PDF_APK/testing/version.txt')
        file.write versionNumber
    }
}

task incVersion() {
    doLast {


        Properties props = new Properties()
        File propsFile = new File('app/gradle.properties')
        props.load(propsFile.newDataInputStream())

        int appCodeNumber = Integer.parseInt(props.getProperty('appCodeNumber'))
        appCodeNumber += 4

        int appVersionNumberIndex = Integer.parseInt(props.getProperty('appVersionNumberIndex'))
        appVersionNumberIndex += 1

        props.setProperty('appCodeNumber', "" + appCodeNumber)
        props.setProperty('appVersionNumberIndex', "" + appVersionNumberIndex)

        props.store(propsFile.newWriter(), null)

        println "My Task: [incVersion]"
    }

}