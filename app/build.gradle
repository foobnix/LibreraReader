apply plugin: 'com.android.application'

ext {
    codeNumber = Integer.parseInt(appCodeNumber)
    versionNumber = appVersionNumberBase + "." + appVersionNumberIndex
    db = Integer.parseInt(appDB)
    println ""
    println "Version: [" + versionNumber + " - " + codeNumber + "]"
    
    
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.foobnix.pdf.reader"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode codeNumber
        versionName versionNumber
        //testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false

    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "LOG", "true"
        }
        debug {
            buildConfigField "boolean", "LOG", "true"
        }


    }
    buildToolsVersion '28.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        // Configures multiple APKs based on ABI.
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk false
        }

    }


    flavorDimensions "version"
    productFlavors {
        beta {
            dimension "version"
            applicationId "com.foobnix.pdf.reader"
            manifestPlaceholders = [
                    appName       : "Beta " + versionNumber,
                    appIcon       : "@mipmap/icon_pdf_reader",
                    appRoundIcon  : "@mipmap/icon_pdf_reader",
                    appGdriveKey  : "961762082517-dgsen03hb73s6oh59ovivcansatu16lc.apps.googleusercontent.com",
                    admobAppId    : "ca-app-pub-8347903083053959~8769894873",
                    admobBannerId : "ca-app-pub-8347903083053959/4166397275",
                    admobFullId   : "ca-app-pub-8347903083053959/2769081274"
            ]
            //buildConfigField "boolean", "LOG", "true"
        }
        librera {
            dimension "version"
            applicationId "com.foobnix.pdf.reader"
            manifestPlaceholders = [
                    appName     : "Librera Reader",
                    appIcon     : "@mipmap/icon_pdf_reader",
                    appRoundIcon: "@mipmap/icon_pdf_reader",
                    appGdriveKey  : "961762082517-dgsen03hb73s6oh59ovivcansatu16lc.apps.googleusercontent.com",
                    admobAppId    : "ca-app-pub-8347903083053959~8769894873",
                    admobBannerId : "ca-app-pub-8347903083053959/4166397275",
                    admobFullId   : "ca-app-pub-8347903083053959/2769081274"
            ]
        }
        pro {
            dimension "version"
            applicationId "com.foobnix.pro.pdf.reader"
            manifestPlaceholders = [
                    appName     : "Librera PRO",
                    appIcon     : "@mipmap/icon_pdf_pro",
                    appRoundIcon: "@mipmap/icon_pdf_pro",
                    appGdriveKey  : "961762082517-ej9mdc7bgp7jkd6hvbfimvda6vpi5p4t.apps.googleusercontent.com",
                    admobAppId    : "",
                    admobBannerId : "",
                    admobFullId   : ""
            ]
        }
        pdf_classic {
            dimension "version"
            applicationId "classic.pdf.reader.viewer.djvu.epub.fb2.txt.mobi.book.reader.lirbi.libri"
            manifestPlaceholders = [
                    appName     : "PDF Reader",
                    appIcon     : "@mipmap/icon_pdf_classic",
                    appRoundIcon: "@mipmap/launcher_classic",
                    appGdriveKey  : "961762082517-5825mr1t8duo7tlnkdr6itersmlreejq.apps.googleusercontent.com",
                    admobAppId    : "ca-app-pub-8347903083053959~8457312875",
                    admobBannerId : "ca-app-pub-8347903083053959/5364245672",
                    admobFullId   : "ca-app-pub-8347903083053959/7763820878"
            ]
        }
        ebooka {
            dimension "version"
            applicationId "droid.reader.book.epub.mobi.pdf.djvu.fb2.txt.azw.azw3"
            manifestPlaceholders = [
                    appName     : "Ebooka Reader",
                    appIcon     : "@mipmap/icon_pdf_droid",
                    appRoundIcon: "@mipmap/launcher_droid",
                    appGdriveKey  : "961762082517-3ud6u3hmjlbbpqdsqtk9md55o5jgkg41.apps.googleusercontent.com",
                    admobAppId    : "ca-app-pub-8347903083053959~5153060505",
                    admobBannerId : "ca-app-pub-8347903083053959/6159730856",
                    admobFullId   : "ca-app-pub-8347903083053959/2346153515"
            ]
        }
        pdf_v2 {
            dimension "version"
            applicationId "pdf.pdf.reader"
            manifestPlaceholders = [
                    appName     : "PDF Reader",
                    appIcon     : "@mipmap/icon_pdf_2",
                    appRoundIcon: "@mipmap/launcher_pdf_v2",
                    appGdriveKey  : "961762082517-eiun09s9u6vferhd9avnkjmgs7ob9tns.apps.googleusercontent.com",
                    admobAppId    : "ca-app-pub-8347903083053959~5943988213",
                    admobBannerId : "ca-app-pub-8347903083053959/6705625294",
                    admobFullId   : "ca-app-pub-8347903083053959/1346267659"
            ]
        }
        tts_reader {
            dimension "version"
            applicationId "tts.reader"
            manifestPlaceholders = [
                    appName     : "TTS Reader",
                    appIcon     : "@mipmap/icon_tts_reader_app",
                    appRoundIcon: "@mipmap/launcher_tts_reader_app",
                    appGdriveKey  : "961762082517-kaap4rki1p1vjdcd2jk5j8o8krckm24a.apps.googleusercontent.com",
                    admobAppId    : "ca-app-pub-8347903083053959~1533135764",
                    admobBannerId : "ca-app-pub-8347903083053959/2702801102",
                    admobFullId   : "ca-app-pub-8347903083053959/4602986382"
            ]
        }
        epub_reader {
            dimension "version"
            applicationId "epub.reader"
            manifestPlaceholders = [
                    appName     : "Epub Reader",
                    appIcon     : "@mipmap/icon_epub_reader",
                    appRoundIcon: "@mipmap/launcher_epub_reader",
                    appGdriveKey  : "961762082517-p6b2kqqimju6rlp314mu56lipvq2m68r.apps.googleusercontent.com",
                    admobAppId    : "ca-app-pub-8347903083053959~8118779310",
                    admobBannerId : "ca-app-pub-8347903083053959/1553370961",
                    admobFullId   : "ca-app-pub-8347903083053959/6163356354"
            ]
        }
        fdroid {
            dimension "version"
            applicationId "com.foobnix.pro.pdf.reader"
            manifestPlaceholders = [
                    appName     : "Librera PRO",
                    appIcon     : "@mipmap/icon_pdf_pro",
                    appRoundIcon: "@mipmap/icon_pdf_pro",
                    appGdriveKey  : "",
                    admobAppId    : "",
                    admobBannerId : "",
                    admobFullId   : ""
            ]
            versionNameSuffix '-fdroid'
        }

    }


    def abiCodes = ['armeabi-v7a': 0, 'arm64-v8a': 1, 'x86': 2, 'x86_64': 3]

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavor = variant.productFlavors[0].name.capitalize()
            def abiName = getFilter("ABI")
            def code = abiCodes.get(abiName, 0) + variant.versionCode
            abiName = abiName.replace("arm64-v8a", "arm64").replace("armeabi-v7a", "arm")

            output.versionCodeOverride = code
            def fullName = "Librera ${flavor}-${versionNumber}-c${code}-d${db}-${abiName}.apk";
            output.outputFileName = fullName;

        }
    }


}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:multidex:1.0.3'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'com.android.support:appcompat-v7:28+'
    implementation 'com.android.support:recyclerview-v7:28+'
    implementation 'com.android.support:cardview-v7:28+'
    implementation 'com.android.support:support-v4:28+'
    implementation 'com.android.support:customtabs:28+'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation ('org.greenrobot:greendao:3.2.2'){
        exclude group: 'org.greenrobot.greendao.rx'
    }
    implementation 'org.greenrobot:greendao-api:3.2.2'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.burgstaller:okhttp-digest:1.18'
    implementation 'com.squareup.okio:okio-parent:1.17.3'
    implementation 'com.github.joniles:rtfparserkit:1.13.0'



    implementation 'org.zwobble.mammoth:mammoth:1.4.0'
    implementation 'javax.xml.stream:stax-api:1.0-2'


    implementation 'com.google.android.gms:play-services-auth:+'
    implementation 'com.google.http-client:google-http-client-gson:+'

    implementation('com.google.api-client:google-api-client-android:+') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:+') {
        exclude group: 'org.apache.httpcomponents'
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    betaImplementation project(':pro')
    //betaImplementation 'com.google.firebase:firebase-ads:17+'
    //betaImplementation 'com.google.firebase:firebase-core:16+'



    libreraImplementation 'com.google.firebase:firebase-ads:17+'
    pdf_v2Implementation 'com.google.firebase:firebase-ads:17+'
    ebookaImplementation 'com.google.firebase:firebase-ads:17+'
    tts_readerImplementation 'com.google.firebase:firebase-ads:17+'
    pdf_classicImplementation 'com.google.firebase:firebase-ads:17+'
    epub_readerImplementation 'com.google.firebase:firebase-ads:17+'

    libreraImplementation 'com.google.firebase:firebase-core:16+'
    pdf_v2Implementation 'com.google.firebase:firebase-core:16+'
    ebookaImplementation 'com.google.firebase:firebase-core:16+'
    tts_readerImplementation 'com.google.firebase:firebase-core:16+'
    pdf_classicImplementation 'com.google.firebase:firebase-core:16+'
    epub_readerImplementation 'com.google.firebase:firebase-core:16+'


    proImplementation 'com.google.firebase:firebase-core:16+'
    proImplementation project(':pro')
    fdroidImplementation project(':pro')

    proImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    betaImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    libreraImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    pdf_v2Implementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    ebookaImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    tts_readerImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    pdf_classicImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    epub_readerImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
}


task copyApks(type: Copy) {
    if (project.hasProperty('beta')) {
        println "My Task: [copy apks]"

        delete fileTree("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing").include("*.apk")

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def flavor = variant.productFlavors[0].name
                println "build/outputs/apk/${flavor}/release/"
                from file(
                        "build/outputs/apk/${flavor}/release/"
                )
                include "*.apk"
                into file("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing/")
            }
        }


        File file = new File('/home/ivan-dev/Dropbox/FREE_PDF_APK/testing/version.txt')
        file.write versionNumber
    }
}

task incVersion() {
    doLast {


        Properties props = new Properties()
        File propsFile = new File('app/gradle.properties')
        props.load(propsFile.newDataInputStream())

        int appCodeNumber = Integer.parseInt(props.getProperty('appCodeNumber'))
        appCodeNumber += 4

        int appVersionNumberIndex = Integer.parseInt(props.getProperty('appVersionNumberIndex'))
        appVersionNumberIndex += 1

        props.setProperty('appCodeNumber', "" + appCodeNumber)
        props.setProperty('appVersionNumberIndex', "" + appVersionNumberIndex)

        props.store(propsFile.newWriter(), null)

        println "My Task: [incVersion]"
    }

}

apply plugin: 'com.google.gms.google-services'