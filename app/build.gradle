plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.googleServices)
}

def taskName = getGradle().getStartParameter().getTaskRequests().toString()


ext {
    FDroidCodeNumber = 6670
    FDroidVersionNumber = "9.2.21"
}


println " --------------------"
println " taskName: " + taskName
println " --------------------"

ext {
    if (!taskName.contains("Fdroid")) {
        codeNumber = Integer.parseInt(appCodeNumber)
        versionNumber = appVersionNumberBase + "." + appVersionNumberIndex
    } else {
        codeNumber = FDroidCodeNumber
        versionNumber = FDroidVersionNumber
    }

    println ""
    println "Version: [" + versionNumber + " - " + codeNumber + "]"
}

android {
    compileSdk = libs.versions.compileSdk.get().toInteger()

    namespace = "com.foobnix.pdf.info"

    defaultConfig {
        namespace = "com.foobnix.pdf.info"
        minSdk = libs.versions.minSdk.get().toInteger()
        targetSdk = libs.versions.targetSdk.get().toInteger()
        versionCode = codeNumber
        versionName = versionNumber
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }

    configurations {
        all {
            exclude module: 'httpclient'
            exclude module: 'commons-logging'
        }
    }

    lint {
        abortOnError = true
        ignoreWarnings = true
        checkReleaseBuilds = taskName.contains("Pro")
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            signingConfig = signingConfigs.release
            debuggable = false
        }
        debug {
            debuggable = true
        }
    }


    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(libs.versions.jvmTarget.get())
        targetCompatibility = JavaVersion.toVersion(libs.versions.jvmTarget.get())
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    splits {
        abi {
            enable = true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk = true
        }
    }

    flavorDimensions.add("version")
    productFlavors {
        fdroid {
            minSdk = libs.versions.minSdk.get().toInteger()
            dimension = "version"
            applicationId = "com.foobnix.pro.pdf.reader"
            manifestPlaceholders = [
                    appName      : "Librera FD",
                    appIcon      : "@mipmap/icon_pdf_pro",
                    appRoundIcon : "@mipmap/icon_pdf_pro",
                    appGdriveKey : "",
                    admobAppId   : "",
                    admobBannerId: "",
                    admobFullId  : "",
                    admobRewardId  : "",
                    appSafeMode  : "true"
            ]
            versionNameSuffix = '-fdroid'
        }
        librera {
            dimension = "version"
            applicationId = "com.foobnix.pdf.reader"
            manifestPlaceholders = [
                    appName      : "Librera",
                    appIcon      : "@mipmap/icon_pdf_reader",
                    appRoundIcon : "@mipmap/icon_pdf_reader",
                    appGdriveKey : librera_appGdriveKey,
                    admobAppId   : librera_admobAppId,
                    admobBannerId: librera_admobBannerId,
                    admobFullId  : librera_admobFullId,
                    admobRewardId  : librera_admobRewardId,
                    appSafeMode  : "false"
            ]
        }

        pro {
            dimension = "version"
            applicationId = "com.foobnix.pro.pdf.reader"
            manifestPlaceholders = [
                    appName      : "Librera",
                    appIcon      : "@mipmap/icon_pdf_pro",
                    appRoundIcon : "@mipmap/icon_pdf_pro",
                    appGdriveKey : pro_appGdriveKey,
                    admobAppId   : "",
                    admobBannerId: "",
                    admobFullId  : "",
                    admobRewardId  : "",
                    appSafeMode  : "false"
            ]
        }
        pdf_classic {
            dimension = "version"
            applicationId = "classic.pdf.reader.viewer.djvu.epub.fb2.txt.mobi.book.reader.lirbi.libri"
            manifestPlaceholders = [
                    appName      : "PDF Reader",
                    appIcon      : "@mipmap/icon_pdf_classic",
                    appRoundIcon : "@mipmap/launcher_classic",
                    appGdriveKey : pdf_classic_appGdriveKey,
                    admobAppId   : pdf_classic_admobAppId,
                    admobBannerId: pdf_classic_admobBannerId,
                    admobFullId  : pdf_classic_admobFullId,
                    admobRewardId: pdf_classic_admobRewardId,
                    appSafeMode  : "true"
            ]
        }
        ebooka {
            dimension = "version"
            applicationId = "droid.reader.book.epub.mobi.pdf.djvu.fb2.txt.azw.azw3"
            manifestPlaceholders = [
                    appName      : "Book Reader",
                    appIcon      : "@mipmap/icon_pdf_droid",
                    appRoundIcon : "@mipmap/launcher_droid",
                    appGdriveKey : ebooka_appGdriveKey,
                    admobAppId   : ebooka_admobAppId,
                    admobBannerId: ebooka_admobBannerId,
                    admobFullId  : ebooka_admobFullId,
                    admobRewardId: ebooka_admobRewardId,
                    appSafeMode  : "false"
            ]
        }
        pdf_v2 {
            dimension = "version"
            applicationId = "pdf.pdf.reader"
            manifestPlaceholders = [
                    appName      : "PDF Reader",
                    appIcon      : "@mipmap/icon_pdf_2",
                    appRoundIcon : "@mipmap/launcher_pdf_v2",
                    appGdriveKey : pdf_v2_appGdriveKey,
                    admobAppId   : pdf_v2_admobAppId,
                    admobBannerId: pdf_v2_admobBannerId,
                    admobFullId  : pdf_v2_admobFullId,
                    admobRewardId: pdf_v2_admobRewardId,
                    appSafeMode  : "false"
            ]
        }
        tts_reader {
            dimension = "version"
            applicationId = "tts.reader"
            manifestPlaceholders = [
                    appName      : "TTS Reader",
                    appIcon      : "@mipmap/icon_tts_reader_app",
                    appRoundIcon : "@mipmap/launcher_tts_reader_app",
                    appGdriveKey : tts_reader_appGdriveKey,
                    admobAppId   : tts_reader_admobAppId,
                    admobBannerId: tts_reader_admobBannerId,
                    admobFullId  : tts_reader_admobFullId,
                    admobRewardId: tts_reader_admobRewardId,
                    appSafeMode  : "false"
            ]
        }
        epub_reader {
            dimension = "version"
            applicationId = "epub.reader"
            manifestPlaceholders = [
                    appName      : "Epub Reader",
                    appIcon      : "@mipmap/icon_epub_reader",
                    appRoundIcon : "@mipmap/launcher_epub_reader",
                    appGdriveKey : epub_reader_appGdriveKey,
                    admobAppId   : epub_reader_admobAppId,
                    admobBannerId: epub_reader_admobBannerId,
                    admobFullId  : epub_reader_admobFullId,
                    admobRewardId: epub_reader_admobRewardId,
                    appSafeMode  : "false"
            ]
        }
    }

    def abiCodes = ['armeabi-v7a': 0, 'arm64-v8a': 1, 'x86': 2, 'x86_64': 3]

    applicationVariants.all { variant ->

        variant.outputs.all { output ->

            def flavor = variant.productFlavors[0].name.capitalize()
            def abiName = getFilter("ABI")
/*            if (abiName == "armeabi-v7a") {
                variant.mergedFlavor.manifestPlaceholders.appSafeMode = "true"
                //variant.resValue "string", "[appSafeMode]", "true"
                println abiName + " : appSafeMode true "
            } else {
                variant.mergedFlavor.manifestPlaceholders.appSafeMode = "true"
                //variant.resValue "string", "[appSafeMode]", "false"
                println abiName + " : appSafeMode false"
            }
*/
            if (abiName != null) {
                def code = abiCodes.get(abiName, 0) + variant.versionCode
                abiName = abiName.replace("arm64-v8a", "arm64").replace("armeabi-v7a", "arm")

                output.versionCodeOverride = code
                //def fullName = "Librera ${flavor}-${versionNumber}-c${code}-d${db}-${abiName}.apk"

                def fullName = "Librera ${flavor}-${versionNumber}-${abiName}.apk"

                output.outputFileName = fullName
            } else {
                //def fullName = "Librera ${flavor}-${versionNumber}-c${codeNumber}-d${db}-universal.apk"
                //def fullName = "Librera ${flavor}-${versionNumber}-universal.apk"
                def fullName = "Librera ${flavor}-${versionNumber}-uni.apk"
                output.versionCodeOverride = codeNumber
                output.outputFileName = fullName
            }
        }
    }

    sourceSets {
        fdroid { assets.srcDirs = ['src/fdroid/assets', 'src/fdroid/assets/'] }
    }

    packagingOptions {
        resources {
            excludes.addAll(
                    'META-INF/DEPENDENCIES',
                    'META-INF/NOTICE',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/NOTICE.txt',
            )
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    proImplementation project(':libPro')
    fdroidImplementation project(':libPro')
    implementation project(':libReflow')

    //compose

    //implementation project(':appLibDrive')


/** AndroidX **/
    implementation(libs.cardview)
    implementation(libs.multidex)
    implementation(libs.recyclerview)
    implementation(libs.work.runtime)
    implementation(libs.legacy.support.v4)
    implementation(libs.appcompat)

    implementation(libs.eventbus)
    implementation(libs.greenrobot.greendao)
    implementation(libs.greendao.api)


    implementation(libs.jsoup)
    implementation(libs.juniversalchardet)

    implementation(libs.okhttp3)
    implementation(libs.okhttp.digest)
    implementation(libs.okio.parent)




    implementation(libs.rtfparserkit)

    implementation(libs.mammoth)
    implementation(libs.stax.api)


    implementation(libs.zip4j)
    implementation(libs.glide)
    annotationProcessor(libs.glide.compiler)

    implementation(libs.commons.logging.api)
    implementation(libs.guava)

    def dep_free = project(':libDepFree')


    libreraImplementation dep_free
    pdf_v2Implementation dep_free
    ebookaImplementation dep_free
    tts_readerImplementation dep_free
    pdf_classicImplementation dep_free
    epub_readerImplementation dep_free
    def dep_pro = project(':libDepPro')


    libreraImplementation dep_pro
    pdf_v2Implementation dep_pro
    ebookaImplementation dep_pro
    tts_readerImplementation dep_pro
    pdf_classicImplementation dep_pro
    epub_readerImplementation dep_pro
    proImplementation dep_pro

/** Testing **/
    testImplementation(libs.junit)
    androidTestImplementation(libs.runner)

}


task copyApks() {
    if (project.hasProperty('beta') || project.hasProperty("release")) {
        println "My Task: [copy apks]"

        //delete fileTree("/home/dev/Dropbox/FREE_PDF_APK/testing").include("*.apk")
        //delete fileTree("/home/dev/Nextcloud/LibreraBeta").include("*.apk")
        delete fileTree("/Users/ivanivanenko/Library/CloudStorage/Dropbox/FREE_PDF_APK/testing").include("*.apk")

        delete fileTree("/Users/ivanivanenko/Library/CloudStorage/Dropbox/FREE_PDF_APK/$versionNumber").include("*.apk")
        //delete fileTree("/home/dev/Dropbox/FREE_PDF_APK/testing/$versionNumber").include("*.apk")

        android.applicationVariants.all { variant ->
            if ("release" == variant.buildType.name) {
                def flavor = variant.productFlavors[0].name

                //println "====  " + variant.buildType.name + " " +  flavor

                def list = []

                if (project.hasProperty("beta")) {
                    //list.add("/home/dev/Nextcloud/LibreraBeta")
                    list.add("/Users/ivanivanenko/Library/CloudStorage/Dropbox/FREE_PDF_APK/testing")
                    //list.add("/home/dev/Dropbox/FREE_PDF_APK/testing")
                }


                if (project.hasProperty("release")) {
                    //list.add("/home/dev/Dropbox/FREE_PDF_APK/testing/$versionNumber")
                    list.add("/Users/ivanivanenko/Library/CloudStorage/Dropbox/FREE_PDF_APK/testing/$versionNumber")
                }
                list.each { dest ->

                    copy {
                        from file(
                                "build/outputs/apk/${flavor}/release/"
                        )
                        include "*.apk"
                        into file(dest)
                        println "Copy: '" + flavor + "' to " + dest

                        eachFile {
                            project.file(dest).mkdirs()
                            if (!file(dest).exists()) {
                                it.exclude()
                            }
                        }

                    }


                }
            }
        }

        //File file = new File('/home/data/Dropbox/FREE_PDF_APK/testing/version.txt')
        //file.write versionNumber
    }
}


task incVersion() {
    doLast {
        Properties props = new Properties()
        File progejectDir = layout.projectDirectory.getAsFile()
        println "progejectDir :" + progejectDir

        File propsFile = new File(progejectDir, 'gradle.properties')

        props.load(propsFile.newDataInputStream())

        int appCodeNumber = Integer.parseInt(props.getProperty('appCodeNumber'))
        appCodeNumber += 4

        int appVersionNumberIndex = Integer.parseInt(props.getProperty('appVersionNumberIndex'))
        appVersionNumberIndex += 1

        props.setProperty('appCodeNumber', "" + appCodeNumber)
        props.setProperty('appVersionNumberIndex', "" + appVersionNumberIndex)

        props.store(propsFile.newWriter(), null)

        println "My Task: [incVersion]"
    }
}
